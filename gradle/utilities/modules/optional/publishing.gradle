//////////////////////////////////////////////////
// TITLE Publishing
//////////////////////////////////////////////////

import static java.io.File.separator

apply from: "$utilitiesDir${separator}meta${separator}application.gradle"
checkApply.call(['createNewFile'])

apply plugin: 'maven-publish'

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the jar output task
// and then declare that to be published
// Note you'll need to add a repository here
publishing {
	publications {
		mavenJava(MavenPublication) {
			project.ext.configurePOM = { Closure action -> action pom }
			project.ext.writePOM = { File destination = project.file 'pom.xml' ->
				pom.withXml {
					createNewFile destination
					destination.setWritable true, false
					destination.text = it.asString().toString()
				}
			}

			from components.java

			if (tasks.findByPath('shadowJar'))
				artifact tasks.shadowJar
			// COMMENT included already, including it again will make the task fail
			// CODE artifact tasks.jar
			artifact tasks.deobfJar
			artifact tasks.javadocJar
			artifact tasks.sourceJar
		}
	}
	repositories {
		if (!repositoryDirectoryLocal.empty) {
			maven {
				url "file:///${rootProject.file(repositoryDirectoryLocal).absolutePath}"
			}
		}
	}
}

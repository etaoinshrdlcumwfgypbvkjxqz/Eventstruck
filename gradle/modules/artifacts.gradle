//////////////////////////////////////////////////
// TITLE Artifacts
//////////////////////////////////////////////////

import static java.io.File.separator

apply from: "$utilitiesDir${separator}meta${separator}application.gradle"
checkApply.call([])

apply plugin: 'com.github.johnrengelman.shadow'

def jar = tasks.named 'jar'
def javadoc = tasks.named 'javadoc'

tasks.register('deobfJar', Jar) {
	dependsOn jar
	from sourceSets.main.java.outputDir
	classifier 'dev'
}
tasks.register('sourceJar', Jar) {
	dependsOn jar
	from sourceSets.main.allSource.srcDirs
	classifier 'source'
}
tasks.register('javadocJar', Jar) {
	dependsOn javadoc
	from javadoc.get().destinationDir
	classifier 'javadoc'
}

stringToStringList(artifactTaskNames).each { String taskName ->
	def task = tasks.named taskName

	task.configure {
		exclude 'dummyThing' // COMMENT dumb dummy
		manifest {
			attributes([
					'Specification-Title'     : modID,
					'Specification-Vendor'    : rootProject.group,
					'Specification-Version'   : version,
					'Implementation-Title'    : project.name,
					'Implementation-Vendor'   : project.group,
					'Implementation-Version'  : version,
			])
			classifier 'thin'
		}

		doFirst {
			manifest {
				attributes([
						'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ'), // COMMENT this should not affect whether the task is up-to-date
				])
			}
		}
	}

	artifacts.archives task
}
